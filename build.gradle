buildscript {
    repositories {
        mavenCentral()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.7.2'
    }
}

allprojects {
    repositories {
        google() // and here
        mavenCentral()
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
        }
    }
}

apply plugin: 'com.android.application'

// Load local.properties file for the sake of signing secrets stored there.
Properties properties = new Properties()
properties.load(project.file('local.properties').newDataInputStream())

android {
    compileSdkVersion 35
    namespace 'io.github.poretsky.tuningfork'

    defaultConfig {
        applicationId "io.github.poretsky.tuningfork"
        minSdkVersion 23
        targetSdkVersion 35
        versionCode 103
        versionName "1.0.3"
    }

    dependenciesInfo {
        // Disables dependency metadata when building APKs.
        includeInApk = false
        // Disables dependency metadata when building Android App Bundles.
        includeInBundle = false
    }

    signingConfigs {
        release {
            // The actual data are taken from the local.properties file.
            keyAlias properties.getProperty('key.alias')
            keyPassword properties.getProperty('key.alias.password')
            storeFile file(properties.getProperty('key.store'))
            storePassword properties.getProperty('key.store.password')
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
            signingConfig signingConfigs.release
            applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = outputFileName.replace("release", defaultConfig.versionName)
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

configurations.all {
    resolutionStrategy {
        eachDependency {
            if ((requested.group == "org.jetbrains.kotlin") && (requested.name.startsWith("kotlin-stdlib"))) {
                useVersion("1.9.0")
            }
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.core:core:1.15.0'
    implementation 'androidx.fragment:fragment:1.8.5'
}
